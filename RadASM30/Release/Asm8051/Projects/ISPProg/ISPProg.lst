                ;*****************************************************
                ;Interrupt vectors.
                ;-----------------------------------------------------
                ;RESET			0000
                ;IE0			0003
                ;TF0			000B
                ;IE1			0013
                ;TF1			001B
                ;RI & TI		0023
                ;TF2 & EXF2		002B
                ;-----------------------------------------------------
                ;*****************************************************
                ;PORT 1
                ;-----------------------------------------------------
                ;P1.0			OUT:	+5V On	
                ;P1.1			OUT:	RST
                ;P1.2			OUT:	SCK
                ;P1.3			OUT:	MISO
                ;P1.4			IN:		MOSI
                ;P1.5
                ;P1.6
                ;P1.7
                ;-----------------------------------------------------
                ;*****************************************************
                ;RAM Locations
                ;-----------------------------------------------------
                ;20				Interrupt flags
                ;21
                ;22
                ;23
                ;24
                ;25				Rom byte to be verified
                ;26				Number of pages
                ;27				PROGROM Action
                ;28				PROGROM Size
                ;29             PROGROM Mode
                ;2A
                ;2B
                ;2C
                ;2D				DPL save
                ;2E				DPH save
                ;2F				Number of verify errors
                ;30-3F
                ;40-4F			Buffer
                ;50-7F			Stack
                ;-----------------------------------------------------
                ;*****************************************************
                ;SCREEN DRIVER
                ;-----------------------------------------------------
                ;01				START SEND ROMDATA.HEX FILE
                ;02				STOP SEND FILE
                ;03				START RECIEVE ROMDATA.HEX FILE
                ;04				STOP RECIEVE FILE
                ;05				START RECIEVE FILE IN 16 BYTE BLOCKS
                ;06				STOP RECIEVE FILE IN 16 BYTE BLOCKS
                ;07				BELL
                ;08				BACK SPACE
                ;09				TAB
                ;0A				LF
                ;0B				LOCATE
                ;0C				HOME
                ;0D				CR
                ;0E				CLS
                ;0F				MODE
                ;10				START SEND CMDFILE.CMD FILE
                ;-----------------------------------------------------
                ;*****************************************************
                				ORG		0000h
0000  01 40     RESET:			AJMP	$START
                ;*****************************************************
0002  FF        				ORG		0003h
0003  20 00 01  IE0IRQ:			JB		$00,$01h				;$20.0
0006  32        				RETI
0007  02 20 03  				LJMP	$2003h
                ;*****************************************************
000A  FF        				ORG		000Bh
000B  20 01 01  TF0IRQ:			JB		$01h,$01h				;$20.1
000E  32        				RETI
000F  02 20 0B  				LJMP	$200Bh
                ;*****************************************************
0012  FF        				ORG		0013h
0013  20 02 01  IE1IRQ:			JB		$02h,$01h				;$20.2
0016  32        				RETI
0017  02 20 13  				LJMP	$2013h
                ;*****************************************************
001A  FF        				ORG		001Bh
001B  20 03 01  TF1IRQ:			JB		$03h,$01h				;$20.3
001E  32        				RETI
001F  02 20 1B  				LJMP	$201Bh
                ;*****************************************************
0022  FF        				ORG		0023h
0023  20 04 01  RITIIRQ:		JB		$04h,$01h				;$20.4
0026  32        				RETI
0027  02 20 23  				LJMP	$2023h
                ;*****************************************************
002A  FF        				ORG		002Bh
002B  20 05 01  TF2EXF2IRQ:		JB		$05h,$01h				;$20.5
002E  32        				RETI
002F  02 20 2B  				LJMP	$202Bh
                ;*****************************************************
                
0032  FF FF FF  				ORG		0040h
                
0040  75 D0 00  START:			MOV		PSW,#00h
0043  75 A8 00  				MOV		IE,#00h					;Disable all int's
0046  75 81 4F  				MOV		SP,#4Fh					;Init stack pointer. The stack is 48 bytes
0049  75 89 22  				MOV		TMOD,#22h				;T0/T1=8 Bit auto reload
004C  75 8C 1A  				MOV		TH0,#1Ah				;256-230
004F  75 8A 1A  				MOV		TL0,#1Ah
0052  75 8D FA  				MOV		TH1,#0FAh				;256-22118400/(384*9600) (#0FF=57600)
0055  75 8B FA  				MOV		TL1,#0FAh
0058  75 87 80  				MOV		PCON,#80h				;Double baudrate
005B  75 98 76  				MOV		SCON,#76h				;SM0=l
                												;SM1=h
                												;SM2=h
                												;REN=h
                												;TB8=h
                												;RB8=l
                												;TI=h
                												;RI=l
005E  75 20 00  				MOV		$20h,#00h				;RAM int routines ($00-$05,$20.0-$20.5)
0061  75 27 01  				MOV		$27h,#01h				;Action
0064  75 28 01  				MOV		$28h,#01h				;Size
0067  75 29 01  				MOV		$29h,#01h				;Mode
006A  75 88 50  				MOV		TCON,#50h				;T0/T1=On
006D  78 00     				MOV		R0,#00h
006F  90 20 00  				MOV		DPTR,#2000h
0072  79 00     				MOV		R1,#00h
0074  D8 FE     START1:			DJNZ	R0,$START1
0076  D9 FC     				DJNZ	R1,$START1
0078  C2 98     				CLR		SCON.0
007A  31 9B     START2:			ACALL	$HELPMENU
007C  11 FB     START3:			ACALL	$PRNTCRLF
007E  74 3E     				MOV		A,#3Eh
0080  31 93     				ACALL	$TXBYTE
0082  31 8B     START4:			ACALL	$RXBYTE
0084  B4 41 06  				CJNE	A,#41h,$START5
                				;Address input
0087  11 F6     				ACALL	$PRNTCMND
0089  51 29     				ACALL	$ADRINPUT
008B  80 EF     				SJMP	$START3
008D  B4 44 06  START5:			CJNE	A,#44h,$START6
                				;Dump
0090  11 F6     				ACALL	$PRNTCMND
0092  51 2C     				ACALL	$DUMP
0094  80 E4     				SJMP	$START2
0096  B4 45 06  START6:			CJNE	A,#45h,$START7
                				;Enter hex
0099  11 F6     				ACALL	$PRNTCMND
009B  51 58     				ACALL	$ENTERHEX
009D  80 DB     				SJMP	$START2
009F  B4 47 06  START7:			CJNE	A,#47h,$START8
                				;Go
00A2  11 F6     				ACALL	$PRNTCMND
00A4  51 C0     				ACALL	$GO
00A6  80 D2     				SJMP	$START2
00A8  B4 48 04  START8:			CJNE	A,#48h,$START9
                				;Help
00AB  11 F6     				ACALL	$PRNTCMND
00AD  80 CB     				SJMP	$START2
00AF  B4 49 06  START9:			CJNE	A,#49h,$START10
                				;Internal memory
00B2  11 F6     				ACALL	$PRNTCMND
00B4  51 6D     				ACALL	$MEMDUMP
00B6  80 C4     				SJMP	$START3
00B8  B4 4C 06  START10:		CJNE	A,#4Ch,$START11
                				;Load
00BB  11 F6     				ACALL	$PRNTCMND
00BD  51 92     				ACALL	$LOAD
00BF  80 BB     				SJMP	$START3
00C1  B4 50 06  START11:		CJNE	A,#50h,$START12
                				;Program ROM
00C4  11 F6     				ACALL	$PRNTCMND
00C6  51 C4     				ACALL	$EPROM
00C8  80 B0     				SJMP	$START2
00CA  B4 52 06  START12:		CJNE	A,#52h,$START13
                				;Run
00CD  11 F6     				ACALL	$PRNTCMND
00CF  51 C2     				ACALL	$RUN
00D1  80 A7     				SJMP	$START2
00D3  B4 0D AC  START13:		CJNE	A,#0Dh,$START4
                				;CR
00D6  80 A4     				SJMP	$START3
                
                ;RS232 Functions
                ;------------------------------------------------------------------
                
00D8  85 82 2D  PRNTSTR:		MOV		$2Dh,DPL
00DB  85 83 2E  				MOV		$2Eh,DPH
00DE  D0 83     				POP		DPH
00E0  D0 82     				POP		DPL
00E2  E4        PRNTSTR1:		CLR		A
00E3  93        				MOVC	A,@A+DPTR
00E4  A3        				INC		DPTR
00E5  60 04     				JZ		$PRNTSTR2
00E7  31 93     				ACALL	$TXBYTE
00E9  80 F7     				SJMP	$PRNTSTR1
00EB  C0 82     PRNTSTR2:		PUSH	DPL
00ED  C0 83     				PUSH	DPH
00EF  85 2D 82  				MOV		DPL,$2Dh
00F2  85 2E 83  				MOV		DPH,$2Eh
00F5  22        				RET
                
00F6  31 93     PRNTCMND:		ACALL	$TXBYTE
00F8  11 FB     				ACALL	$PRNTCRLF
00FA  22        				RET
                
00FB  74 0D     PRNTCRLF:		MOV		A,#0Dh
00FD  31 93     				ACALL	$TXBYTE
00FF  74 0A     				MOV		A,#0Ah
0101  31 93     				ACALL	$TXBYTE
0103  22        				RET
                
0104  C0 E0     HEXOUT:			PUSH	ACC
0106  C4        				SWAP	A
0107  31 0B     				ACALL	$HEXOUT1
0109  D0 E0     				POP		ACC
010B  54 0F     HEXOUT1:		ANL		A,#0Fh
010D  C3        				CLR		C
010E  94 0A     				SUBB	A,#0Ah
0110  40 02     				JC		$HEXOUT2
0112  24 07     				ADD		A,#07h
0114  24 3A     HEXOUT2:		ADD		A,#3Ah
0116  31 93     				ACALL	$TXBYTE
0118  22        				RET
                
0119  E5 83     HEXDPTR:		MOV		A,DPH
011B  31 04     				ACALL	$HEXOUT
011D  E5 82     				MOV		A,DPL
011F  31 04     				ACALL	$HEXOUT
0121  74 20     				MOV		A,#20h
0123  31 93     				ACALL	$TXBYTE
0125  22        				RET
                
0126  31 32     HEXINPBYTE:		ACALL	$HEXINP
0128  40 07     				JC		$HEXINPBYTE1
012A  C4        				SWAP	A
012B  FB        				MOV		R3,A
012C  31 32     				ACALL	$HEXINP
012E  40 01     				JC		$HEXINPBYTE1
0130  2B        				ADD		A,R3
0131  22        HEXINPBYTE1:	RET
                
0132  31 3E     HEXINP:			ACALL	$HEXINP2
0134  40 07     				JC		$HEXINP1
0136  C0 E0     				PUSH	ACC
0138  EA        				MOV		A,R2
0139  31 93     				ACALL	$TXBYTE
013B  D0 E0     				POP		ACC
013D  22        HEXINP1:		RET
                
013E  31 8B     HEXINP2:		ACALL	$RXBYTE
0140  B4 9F 02  				CJNE	A,#9Fh,$HEXINP3			;Esc
0143  D3        				SETB	C
0144  22        				RET
0145  B4 0D 02  HEXINP3:		CJNE	A,#0Dh,$HEXINP4			;Cr
0148  D3        				SETB	C
0149  22        				RET
014A  FA        HEXINP4:		MOV		R2,A
014B  B4 3A 00  				CJNE	A,#3Ah,$00h
014E  50 08     				JNC		$HEXINP5
0150  B4 30 00  				CJNE	A,#30h,$00h
0153  40 E9     				JC		$HEXINP2
0155  94 30     				SUBB	A,#30h
0157  22        				RET
0158  B4 47 00  HEXINP5:		CJNE	A,#47h,$00h
015B  50 E1     				JNC		$HEXINP2
015D  B4 41 00  				CJNE	A,#41h,$00h
0160  40 DC     				JC		$HEXINP2
0162  94 37     				SUBB	A,#37h
0164  22        				RET
                
0165  31 19     INPDPTR:		ACALL	$HEXDPTR
0167  31 26     				ACALL	$HEXINPBYTE
0169  40 08     				JC		$INPDPTR1
016B  F5 83     				MOV		DPH,A
016D  31 26     				ACALL	$HEXINPBYTE
016F  40 02     				JC		$INPDPTR1
0171  F5 82     				MOV		DPL,A
0173  11 FB     INPDPTR1:		ACALL	$PRNTCRLF
0175  22        				RET
                
0176  C0 01     RX16BYTES:		PUSH	$01h
0178  74 05     				MOV		A,#05h
017A  31 93     				ACALL	$TXBYTE
017C  78 40     				MOV		R0,#40h
017E  79 10     				MOV		R1,#10h
0180  31 8B     RX16BYTES1:		ACALL	$RXBYTE
0182  F6        				MOV		@R0,A
0183  08        				INC		R0
0184  D9 FA     				DJNZ	R1,$RX16BYTES1
0186  D0 01     				POP		$01h
0188  78 40     				MOV		R0,#40h
018A  22        				RET
                
018B  30 98 FD  RXBYTE:			JNB		SCON.0,$RXBYTE
018E  C2 98     				CLR		SCON.0
0190  E5 99     				MOV		A,SBUF
0192  22        				RET
                
0193  30 99 FD  TXBYTE:			JNB		SCON.1,$TXBYTE
0196  C2 99     				CLR		SCON.1
0198  F5 99     				MOV		SBUF,A
019A  22        				RET
                
                ;Functions
                ;------------------------------------------------------------------
                
019B  11 D8     HELPMENU:		ACALL	$PRNTSTR
019D  0E        				DB		0Eh
019E  41 20 41  				DB		'A Address input',0Dh,0Ah
01AF  44 20 44  				DB		'D Dump as hex',0Dh,0Ah
01BE  45 20 45  				DB		'E Enter hex',0Dh,0Ah
01CB  47 20 47  				DB		'G Go (Load and Run)',0Dh,0Ah
01E0  48 20 48  				DB		'H Help',0Dh,0Ah
01E8  49 20 49  				DB		'I Internal memory dump',0Dh,0Ah
0200  4C 20 4C  				DB		'L Load cmd file',0Dh,0Ah
0211  50 20 50  				DB		'P Program ROM',0Dh,0Ah
0220  52 20 52  				DB		'R Run',0Dh,0Ah,00h
0228  22        				RET
                
0229  31 65     ADRINPUT:		ACALL	$INPDPTR
022B  22        				RET
                
022C  C0 82     DUMP:			PUSH	DPL
022E  C0 83     				PUSH	DPH
0230  C0 02     				PUSH	$02h
0232  C0 03     				PUSH	$03h
0234  7B 10     DUMP1:			MOV		R3,#10h
0236  7A 10     DUMP2:			MOV		R2,#10h
0238  31 19     				ACALL	$HEXDPTR
023A  E0        DUMP3:			MOVX	A,@DPTR
023B  31 04     				ACALL	$HEXOUT
023D  74 20     				MOV		A,#20h
023F  31 93     				ACALL	$TXBYTE
0241  A3        				INC		DPTR
0242  DA F6     				DJNZ	R2,$DUMP3
0244  11 FB     				ACALL	$PRNTCRLF
0246  DB EE     				DJNZ	R3,$DUMP2
0248  11 FB     				ACALL	$PRNTCRLF
024A  31 8B     				ACALL	$RXBYTE
024C  B4 9F E5  				CJNE	A,#9Fh,$DUMP1			;Esc
024F  D0 03     				POP		$03h
0251  D0 02     				POP		$02h
0253  D0 83     				POP		DPH
0255  D0 82     				POP		DPL
0257  22        				RET
                
0258  C0 82     ENTERHEX:		PUSH	DPL
025A  C0 83     				PUSH	DPH
025C  31 19     ENTERHEX1:		ACALL	$HEXDPTR
025E  31 26     				ACALL	$HEXINPBYTE
0260  40 06     				JC		$ENTERHEX2
0262  F0        				MOVX	@DPTR,A
0263  A3        				INC		DPTR
0264  11 FB     				ACALL	$PRNTCRLF
0266  80 F4     				SJMP	$ENTERHEX1
0268  D0 83     ENTERHEX2:		POP		DPH
026A  D0 82     				POP		DPL
026C  22        				RET
                
026D  C0 00     MEMDUMP:		PUSH	$00h
026F  78 00     				MOV		R0,#00h
0271  E4        MEMDUMP1:		CLR		A
0272  31 04     				ACALL	$HEXOUT
0274  E8        				MOV		A,R0
0275  31 04     				ACALL	$HEXOUT
0277  74 20     				MOV		A,#20h
0279  31 93     				ACALL	$TXBYTE
027B  E6        MEMDUMP2:		MOV		A,@R0
027C  31 04     				ACALL	$HEXOUT
027E  74 20     				MOV		A,#20h
0280  31 93     				ACALL	$TXBYTE
0282  08        				INC		R0
0283  E8        				MOV		A,R0
0284  54 0F     				ANL		A,#0Fh
0286  70 F3     				JNZ		$MEMDUMP2
0288  11 FB     				ACALL	$PRNTCRLF
028A  E8        				MOV		A,R0
028B  64 80     				XRL		A,#80h
028D  70 E2     				JNZ		$MEMDUMP1
028F  D0 00     				POP		$00h
0291  22        				RET
                
0292  C0 82     LOAD:			PUSH	DPL
0294  C0 83     				PUSH	DPH
0296  C0 00     				PUSH	$00h
0298  C0 03     				PUSH	$03h
029A  7B 80     				MOV		R3,#80h
029C  31 76     LOAD1:			ACALL	$RX16BYTES				;Read 16 bytes from cmd file
029E  31 19     				ACALL	$HEXDPTR
02A0  E6        LOAD2:			MOV		A,@R0
02A1  F0        				MOVX	@DPTR,A
02A2  31 04     				ACALL	$HEXOUT
02A4  74 20     				MOV		A,#20h
02A6  31 93     				ACALL	$TXBYTE
02A8  A3        				INC		DPTR
02A9  08        				INC		R0
02AA  E8        				MOV		A,R0
02AB  64 50     				XRL		A,#50h
02AD  70 F1     				JNZ		$LOAD2					;Not 16 bytes yet
02AF  11 FB     				ACALL	$PRNTCRLF
02B1  DB E9     				DJNZ	R3,$LOAD1				;Not 2K yet
02B3  74 06     				MOV		A,#06h
02B5  31 93     				ACALL	$TXBYTE					;End read 16 bytes from cmd file
02B7  D0 03     				POP		$03h
02B9  D0 00     				POP		$00h
02BB  D0 83     				POP		DPH
02BD  D0 82     				POP		DPL
02BF  22        				RET
                
02C0  51 92     GO:				ACALL	$LOAD
02C2  E4        RUN:			CLR		A
02C3  73        				JMP		@A+DPTR
                
                ;ROM menu selection
                ;------------------------------------------------------------------
                
02C4  71 2C     EPROM:			ACALL	$ROMMENU
02C6  B4 94 62  				CJNE	A,#94h,$EPROMEXIT
02C9  B1 BC     				ACALL	$ROMINSERT
02CB  40 F7     				JC		$EPROM
02CD  12 09 D0  				LCALL	$ROMINIT				;Turn on VCC, pull RST high and init programming mode
02D0  40 F2     				JC		$EPROM					;Initialisation failed
02D2  E5 27     				MOV		A,$27h
02D4  14        				DEC		A
02D5  70 0F     				JNZ		$EPROM2
                				;Test erased
02D7  D1 5D     				ACALL	$ROMWAIT
02D9  E5 29     				MOV		A,$29h
02DB  14        				DEC		A
02DC  70 04     				JNZ		$EPROM1
02DE  D1 6C     				ACALL	$BM_ROMERASED
02E0  80 E2     				SJMP	$EPROM
02E2  F1 3E     EPROM1:			ACALL	$PM_ROMERASED
02E4  80 DE     				SJMP	$EPROM
02E6  14        EPROM2:			DEC		A
02E7  70 0F     				JNZ		$EPROM3
                				;Dump to hex file
02E9  D1 5D     				ACALL	$ROMWAIT
02EB  E5 29     				MOV		A,$29h
02ED  14        				DEC		A
02EE  70 04     				JNZ		$EPROM21
02F0  D1 B3     				ACALL	$BM_ROMDUMPF
02F2  80 D0     				SJMP	$EPROM
02F4  F1 90     EPROM21:		ACALL	$PM_ROMDUMPF
02F6  80 CC     				SJMP	$EPROM
02F8  14        EPROM3:			DEC		A
02F9  70 0D     				JNZ		$EPROM4
                				;Dump to screen
02FB  E5 29     				MOV		A,$29h
02FD  14        				DEC		A
02FE  70 04     				JNZ		$EPROM31
0300  D1 DA     				ACALL	$BM_ROMDUMPS
0302  80 C0     				SJMP	$EPROM
0304  F1 C3     EPROM31:		ACALL	$PM_ROMDUMPS
0306  80 BC     				SJMP	$EPROM
0308  14        EPROM4:			DEC		A
0309  70 10     				JNZ		$EPROM5
                				;Verify
030B  D1 5D     				ACALL	$ROMWAIT
030D  E5 29     				MOV		A,$29h
030F  14        				DEC		A
0310  70 04     				JNZ		$EPROM41
0312  F1 02     				ACALL	$BM_ROMVERIFY
0314  80 AE     				SJMP	$EPROM
0316  12 07 F6  EPROM41:		LCALL	$PM_ROMVERIFY
0319  80 A9     				SJMP	$EPROM
                EPROM5:			;Program
031B  D1 5D     				ACALL	$ROMWAIT
031D  E5 29     				MOV		A,$29h
031F  14        				DEC		A
0320  70 04     				JNZ		$EPROM51
0322  F1 3D     				ACALL	$BM_ROMPROG
0324  80 9E     				SJMP	$EPROM
0326  12 08 6D  EPROM51:		LCALL	$PM_ROMPROG
0329  80 99     				SJMP	$EPROM
032B  22        EPROMEXIT:		RET
                
032C  11 D8     ROMMENU:		ACALL	$PRNTSTR
032E  0E        				DB		0Eh
032F  20 20 20  				DB		'   ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿  ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿  ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿',0Dh,0Ah
036B  20 20 20  				DB		'   ³  Action       ³  ³  EEPROM size  ³  ³  Mode         ³',0Dh,0Ah
03A7  20 20 20  				DB		'   ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´  ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´  ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´',0Dh,0Ah
03E3  20 20 20  				DB		'   ³  Test erased  ³  ³  4K           ³  ³  Byte         ³',0Dh,0Ah
041F  20 20 20  				DB		'   ³  Filedump     ³  ³  8K           ³  ³  Page         ³',0Dh,0Ah
045B  20 20 20  				DB		'   ³  Screendump   ³  ³  16K          ³  ³               ³',0Dh,0Ah
0497  20 20 20  				DB		'   ³  Verify       ³  ³  32K          ³  ³               ³',0Dh,0Ah
04D3  20 20 20  				DB		'   ³  Program      ³  ³  64K          ³  ³               ³',0Dh,0Ah
050F  20 20 20  				DB		'   ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ',0Dh,0Ah
054B  00        				DB		00h
054C  78 28     				MOV		R0,#28h
054E  B1 9F     				ACALL	$MENUSET
0550  08        				INC		R0
0551  B1 9F     				ACALL	$MENUSET
0553  78 27     				MOV		R0,#27h
0555  B1 58     				ACALL	$MENUXP
0557  22        				RET
                
0558  B1 9F     MENUXP:			ACALL	$MENUSET
055A  74 08     				MOV		A,#08h
055C  12 01 93  				LCALL	$TXBYTE
055F  12 01 8B  				LCALL	$RXBYTE
0562  B4 9A 0C  				CJNE	A,#9Ah,$MENUXP1
0565  E6        				MOV		A,@R0
0566  14        				DEC		A
0567  60 EF     				JZ		$MENUXP
0569  74 20     				MOV		A,#20h
056B  12 01 93  				LCALL	$TXBYTE
056E  16        				DEC		@R0
056F  80 E7     				SJMP	$MENUXP
0571  B4 9B 0D  MENUXP1:		CJNE	A,#9Bh,$MENUXP2
0574  E6        				MOV		A,@R0
0575  94 05     				SUBB	A,#05h
0577  60 DF     				JZ		$MENUXP
0579  74 20     				MOV		A,#20h
057B  12 01 93  				LCALL	$TXBYTE
057E  06        				INC		@R0
057F  80 D7     				SJMP	$MENUXP
0581  B4 9C 08  MENUXP2:		CJNE	A,#9Ch,$MENUYP1
0584  E8        				MOV		A,R0
0585  94 29     				SUBB	A,#29h
0587  60 CF     				JZ		$MENUXP
0589  08        				INC		R0
058A  80 CC     				SJMP	$MENUXP
058C  B4 9D 08  MENUYP1:		CJNE	A,#9Dh,$MENUYP2
058F  E8        				MOV		A,R0
0590  94 27     				SUBB	A,#27h
0592  60 C4     				JZ		$MENUXP
0594  18        				DEC		R0
0595  80 C1     				SJMP	$MENUXP
0597  B4 9F 01  MENUYP2:		CJNE	A,#9Fh,$MENUYP3			;Esc
059A  22        				RET
059B  B4 94 BA  MENUYP3:		CJNE	A,#94h,$MENUXP			;Insert
059E  22        				RET
                
059F  74 0B     MENUSET:		MOV		A,#0Bh
05A1  12 01 93  				LCALL	$TXBYTE
05A4  E6        				MOV		A,@R0
05A5  24 22     				ADD		A,#22h
05A7  12 01 93  				LCALL	$TXBYTE
05AA  E8        				MOV		A,R0
05AB  94 27     				SUBB	A,#27h
05AD  75 F0 13  				MOV		B,#13h
05B0  A4        				MUL		AB
05B1  24 24     				ADD		A,#24h
05B3  12 01 93  				LCALL	$TXBYTE
05B6  74 FB     				MOV		A,#0FBh					;û
05B8  12 01 93  				LCALL	$TXBYTE
05BB  22        				RET
                
                ;------------------------------------------------------------------
                
05BC  12 09 80  ROMINSERT:		LCALL	$ROMOFF
05BF  11 D8     				ACALL	$PRNTSTR
05C1  0B 2A 23  				DB		0Bh,2Ah,23h,'Insert ',00h
05CC  E5 28     				MOV		A,$28h
05CE  14        				DEC		A
05CF  70 08     				JNZ		$ROMINSERT1
05D1  11 D8     				ACALL	$PRNTSTR
05D3  34 4B 00  				DB		'4K',00h
05D6  75 26 10  				MOV		$26h,#10h				;16 Pages
05D9  14        ROMINSERT1:		DEC		A
05DA  70 08     				JNZ		$ROMINSERT2
05DC  11 D8     				ACALL	$PRNTSTR
05DE  38 4B 00  				DB		'8K',00h
05E1  75 26 20  				MOV		$26h,#20h				;32 Pages
05E4  14        ROMINSERT2:		DEC		A
05E5  70 09     				JNZ		$ROMINSERT3
05E7  11 D8     				ACALL	$PRNTSTR
05E9  31 36 4B  				DB		'16K',00h
05ED  75 26 40  				MOV		$26h,#40h				;64 Pages
05F0  14        ROMINSERT3:		DEC		A
05F1  70 09     				JNZ		$ROMINSERT4
05F3  11 D8     				ACALL	$PRNTSTR
05F5  33 32 4B  				DB		'32K',00h
05F9  75 26 80  				MOV		$26h,#80h				;128 Pages
05FC  14        ROMINSERT4:		DEC		A
05FD  70 09     				JNZ		$ROMINSERT5
05FF  11 D8     				ACALL	$PRNTSTR
0601  36 34 4B  				DB		'64K',00h
0605  75 26 00  				MOV		$26h,#00h				;256 Pages
0608  11 D8     ROMINSERT5:		ACALL	$PRNTSTR
060A  20 64 65  				DB		' device and strike <Enter> ',00h
0626  31 8B     				ACALL	$RXBYTE
0628  B4 9F 02  				CJNE	A,#9Fh,$ROMINSERT6	;Esc
062B  D3        				SETB	C
062C  22        				RET
062D  11 D8     ROMINSERT6:		ACALL	$PRNTSTR
062F  0B 2A 23  				DB		0Bh,2Ah,23h,'                                       '
0659  0D 00     				DB		0Dh,00h
065B  C3        				CLR		C
065C  22        				RET
                
065D  11 D8     ROMWAIT:		ACALL	$PRNTSTR
065F  0B 2A 23  				DB		0Bh,2Ah,23h,'Wait ...',00h
066B  22        				RET
                
                
                ;Byte mode
                ;------------------------------------------------------------------
                
066C  12 09 E0  BM_ROMERASED:	LCALL	$BM_ROMRDBYTE			;Read a byte from ROM
066F  B4 FF 0E  				CJNE	A,#0FFh,$BM_ROMERASED1	;Not erased
0672  A3        				INC		DPTR					;Next address
0673  E5 82     				MOV		A,DPL
0675  70 F5     				JNZ		$BM_ROMERASED			;Jump if more bytes in this page
0677  E5 83     				MOV		A,DPH
0679  B5 26 F0  				CJNE	A,$26h,$BM_ROMERASED	;Jump if more pages
067C  12 09 80  				LCALL	$ROMOFF					;Set RST low and turn off VCC
067F  22        				RET
0680  12 09 80  BM_ROMERASED1:	LCALL	$ROMOFF					;Set RST low and turn off VCC
0683  11 D8     				ACALL	$PRNTSTR
0685  0B 2A 23  				DB		0Bh,2Ah,23h,'Byte at ',00h
0691  E5 83     				MOV		A,DPH
0693  31 04     				ACALL	$HEXOUT					;High address
0695  E5 82     				MOV		A,DPL
0697  31 04     				ACALL	$HEXOUT					;Low address
0699  11 D8     				ACALL	$PRNTSTR
069B  20 6E 6F  				DB		' not erased <Enter> ',00h
06B0  31 8B     				ACALL	$RXBYTE					;Wait for keypress
06B2  22        				RET
                
06B3  74 03     BM_ROMDUMPF:	MOV		A,#03h
06B5  31 93     				ACALL	$TXBYTE					;Init write to file
06B7  12 09 E0  BM_ROMDUMPF1:	LCALL	$BM_ROMRDBYTE			;Read a byte from ROM
06BA  31 04     				ACALL	$HEXOUT					;Output as hex
06BC  74 20     				MOV		A,#20h
06BE  31 93     				ACALL	$TXBYTE					;Output a space
06C0  A3        				INC		DPTR
06C1  E5 82     				MOV		A,DPL
06C3  54 0F     				ANL		A,#0Fh
06C5  70 02     				JNZ		$BM_ROMDUMPF2			;Still on same line
06C7  11 FB     				ACALL	$PRNTCRLF				;Output CRLF
06C9  E5 82     BM_ROMDUMPF2:	MOV		A,DPL
06CB  70 EA     				JNZ		$BM_ROMDUMPF1			;Jump if more bytes in this page
06CD  E5 83     				MOV		A,DPH
06CF  B5 26 E5  				CJNE	A,$26h,$BM_ROMDUMPF1	;Jump if more pages
06D2  74 04     				MOV		A,#04h
06D4  31 93     				ACALL	$TXBYTE					;End write to file
06D6  12 09 80  				LCALL	$ROMOFF					;Set RST low and turn off VCC
06D9  22        BM_ROMDUMPF3:	RET
                
06DA  12 09 E0  BM_ROMDUMPS:	LCALL	$BM_ROMRDBYTE			;Read a byte from ROM
06DD  31 04     				ACALL	$HEXOUT					;Output as hex
06DF  74 20     				MOV		A,#20h
06E1  31 93     				ACALL	$TXBYTE					;Output a space
06E3  A3        				INC		DPTR					;Next ROM address
06E4  E5 82     				MOV		A,DPL
06E6  54 0F     				ANL		A,#0Fh
06E8  70 02     				JNZ		$BM_ROMDUMPS1			;Jump if still on same line
06EA  11 FB     				ACALL	$PRNTCRLF				;Output CRLF
06EC  E5 82     BM_ROMDUMPS1:	MOV		A,DPL
06EE  70 EA     				JNZ		$BM_ROMDUMPS			;Jump if more bytes in this page
06F0  11 FB     				ACALL	$PRNTCRLF				;Output CRLF
06F2  31 8B     				ACALL	$RXBYTE					;Wait for a keypress
06F4  B4 9F 02  				CJNE	A,#9Fh,$BM_ROMDUMPS2
06F7  80 05     				SJMP	$BM_ROMDUMPS3
06F9  E5 83     BM_ROMDUMPS2:	MOV		A,DPH
06FB  B5 26 DC  				CJNE	A,$26h,$BM_ROMDUMPS		;Jump if more pages
06FE  12 09 80  BM_ROMDUMPS3:	LCALL	$ROMOFF					;Set RST low and turn off VCC
0701  22        				RET
                
0702  C0 00     BM_ROMVERIFY:	PUSH	$00h					;Save R0
0704  75 2F 00  				MOV		$2Fh,#00h				;Number of errors
0707  31 76     				ACALL	$RX16BYTES				;Read 16 bytes from cmd file. R0 points to start of 16 byte buffer
0709  86 25     BM_ROMVERIFY1:	MOV		$25h,@R0				;Get byte from buffer
070B  12 09 E0  				LCALL	$BM_ROMRDBYTE			;Read a byte from ROM
070E  B5 25 1B  				CJNE	A,$25h,$BM_ROMVERIFY4	;Compare and jump if not equal
0711  08        BM_ROMVERIFY2:	INC		R0						;Increment buffer pointer
0712  E8        				MOV		A,R0
0713  B4 50 02  				CJNE	A,#50h,$BM_ROMVERIFY3	;Jump if not last byte in buffer
0716  31 76     				ACALL	$RX16BYTES				;Read 16 bytes from cmd file. R0 points to start of 16 byte buffer
0718  A3        BM_ROMVERIFY3:	INC		DPTR					;Next ROM address
0719  E5 82     				MOV		A,DPL
071B  70 EC     				JNZ		$BM_ROMVERIFY1			;Jump if still on same page
071D  E5 83     				MOV		A,DPH
071F  B5 26 E7  				CJNE	A,$26h,$BM_ROMVERIFY1	;Jump if more pages
0722  12 09 80  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0725  74 06     				MOV		A,#06h
0727  31 93     				ACALL	$TXBYTE					;End read 16 bytes from cmd file
0729  D0 00     				POP		$00h					;Restore R0
072B  22        				RET
072C  12 09 F5  BM_ROMVERIFY4:	LCALL	$ROMVERIFYERR
072F  50 E0     				JNC		$BM_ROMVERIFY2			;Jump if less than 16 errors
0731  12 09 80  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0734  74 06     				MOV		A,#06h
0736  31 93     				ACALL	$TXBYTE					;End read 16 bytes from cmd file
0738  31 8B     				ACALL	$RXBYTE					;Wait for a keypress
073A  D0 00     				POP		$00h					;Restore R0
073C  22        				RET
                
                BM_ROMPROG:
073D  22        				RET
                
                ;Page mode
                ;------------------------------------------------------------------
                
073E  74 30     PM_ROMERASED:	MOV		A,#30h
0740  12 09 58  				LCALL	$ISPCOMM				;Init read page mode
0743  E5 83     				MOV		A,DPH
0745  12 09 58  				LCALL	$ISPCOMM				;Send high address
0748  E4        PM_ROMERASED1:	CLR		A
0749  12 09 58  				LCALL	$ISPCOMM				;Get byte from ROM
074C  B4 FF 0E  				CJNE	A,#0FFh,$PM_ROMERASED2	;Jump if not erased
074F  A3        				INC		DPTR
0750  E5 82     				MOV		A,DPL
0752  70 F4     				JNZ		$PM_ROMERASED1			;Jump if more bytes on this page
0754  E5 83     				MOV		A,DPH
0756  B5 26 E5  				CJNE	A,$26h,$PM_ROMERASED		;Jump if more pagees
0759  12 09 80  				LCALL	$ROMOFF					;Set RST low and turn off VCC
075C  22        				RET
075D  12 09 80  PM_ROMERASED2:	LCALL	$ROMOFF					;Set RST low and turn off VCC
0760  11 D8     				ACALL	$PRNTSTR
0762  0B 2A 23  				DB		0Bh,2Ah,23h,'Byte at ',00h
076E  E5 83     				MOV		A,DPH
0770  31 04     				ACALL	$HEXOUT					;Output high address as hex
0772  E5 82     				MOV		A,DPL
0774  31 04     				ACALL	$HEXOUT					;Output low address as hex
0776  11 D8     				ACALL	$PRNTSTR
0778  20 6E 6F  				DB		' not erased <Enter> ',00h
078D  31 8B     				ACALL	$RXBYTE					;Wait for a keypress
078F  22        				RET
                
0790  74 03     PM_ROMDUMPF:	MOV		A,#03h
0792  31 93     				ACALL	$TXBYTE					;Init Output to hex file
0794  74 30     PM_ROMDUMPF1:	MOV		A,#30h
0796  12 09 58  				LCALL	$ISPCOMM				;Init read page mode
0799  E5 83     				MOV		A,DPH
079B  12 09 58  				LCALL	$ISPCOMM				;Send high address
079E  E4        PM_ROMDUMPF2:	CLR		A
079F  12 09 58  				LCALL	$ISPCOMM				;Get byte from ROM
07A2  31 04     				ACALL	$HEXOUT					;Output as hex
07A4  74 20     				MOV		A,#20h
07A6  31 93     				ACALL	$TXBYTE					;Output a space
07A8  A3        				INC		DPTR
07A9  E5 82     				MOV		A,DPL
07AB  54 0F     				ANL		A,#0Fh
07AD  B4 00 06  				CJNE	A,#00h,$PM_ROMDUMPF3	;Jump if more bytes in this line
07B0  11 FB     				ACALL	$PRNTCRLF				;Output CRLF
07B2  E5 82     				MOV		A,DPL
07B4  70 E8     				JNZ		$PM_ROMDUMPF2			;Jump if more bytes in this page
07B6  E5 83     PM_ROMDUMPF3:	MOV		A,DPH
07B8  B5 26 D9  				CJNE	A,$26h,$PM_ROMDUMPF1	;Jump if more pages
07BB  74 04     				MOV		A,#04h
07BD  31 93     				ACALL	$TXBYTE					;End Output to hex file
07BF  12 09 80  				LCALL	$ROMOFF					;Set RST low and turn off VCC
07C2  22        				RET
                
07C3  74 30     PM_ROMDUMPS:	MOV		A,#30h
07C5  12 09 58  				LCALL	$ISPCOMM				;Init read page mode
07C8  E5 83     				MOV		A,DPH
07CA  12 09 58  				LCALL	$ISPCOMM				;Send high address
07CD  E4        PM_ROMDUMPS1:	CLR		A
07CE  12 09 58  				LCALL	$ISPCOMM				;Get byte from ROM
07D1  31 04     				ACALL	$HEXOUT					;Output as hex
07D3  74 20     				MOV		A,#20h
07D5  31 93     				ACALL	$TXBYTE					;Output a space
07D7  A3        				INC		DPTR
07D8  E5 82     				MOV		A,DPL
07DA  54 0F     				ANL		A,#0Fh
07DC  70 0F     				JNZ		$PM_ROMDUMPS2			;Jump if still on same line
07DE  11 FB     				ACALL	$PRNTCRLF				;Output CRLF
07E0  E5 82     				MOV		A,DPL
07E2  70 E9     				JNZ		$PM_ROMDUMPS1			;Jump if more bytes on this page
07E4  11 FB     				ACALL	$PRNTCRLF				;Output CRLF
07E6  31 8B     				ACALL	$RXBYTE					;Wait for a keypress
07E8  B4 9F 02  				CJNE	A,#9Fh,$PM_ROMDUMPS2
07EB  80 05     				SJMP	$PM_ROMDUMPS3			;Esc pressed
07ED  E5 83     PM_ROMDUMPS2:	MOV		A,DPH
07EF  B5 26 D1  				CJNE	A,$26h,$PM_ROMDUMPS		;Jump if more pages
07F2  12 09 80  PM_ROMDUMPS3:	LCALL	$ROMOFF					;Set RST low and turn off VCC
07F5  22        				RET
                
07F6  C0 00     PM_ROMVERIFY:	PUSH	$00h					;Save R0
07F8  75 2F 00  				MOV		$2Fh,#00h				;Number of errors
07FB  12 01 76  				LCALL	$RX16BYTES				;Read 16 bytes from cmd file. R0 points to start of 16 byte buffer
07FE  74 30     PM_ROMVERIFY1:	MOV		A,#30h
0800  12 09 58  				LCALL	$ISPCOMM				;Init read page mode
0803  E5 83     				MOV		A,DPH
0805  12 09 58  				LCALL	$ISPCOMM				;Send high address
0808  86 25     PM_ROMVERIFY2:	MOV		$25h,@R0				;Get byte from buffer
080A  E4        				CLR		A
080B  12 09 58  				LCALL	$ISPCOMM				;Get byte from ROM
080E  B5 25 1D  				CJNE	A,$25h,$PM_ROMVERIFY5	;Compare and jump if not equal
0811  08        PM_ROMVERIFY3:	INC		R0						;Increment buffer pointer
0812  E8        				MOV		A,R0
0813  B4 50 03  				CJNE	A,#50h,$PM_ROMVERIFY4	;Jump if not last byte in buffer
0816  12 01 76  				LCALL	$RX16BYTES				;Read 16 bytes from cmd file. R0 points to start of 16 byte buffer
0819  A3        PM_ROMVERIFY4:	INC		DPTR
081A  E5 82     				MOV		A,DPL
081C  70 EA     				JNZ		$PM_ROMVERIFY2			;Jump if still on same page
081E  E5 83     				MOV		A,DPH
0820  B5 26 DB  				CJNE	A,$26h,$PM_ROMVERIFY1	;Jump if more pages
0823  12 09 80  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0826  74 06     				MOV		A,#06h
0828  12 01 93  				LCALL	$TXBYTE					;End read 16 bytes from cmd file
082B  D0 00     				POP		$00h					;Restore R0
082D  22        				RET
082E  12 09 F5  PM_ROMVERIFY5:	LCALL	$ROMVERIFYERR
0831  50 DE     				JNC		$PM_ROMVERIFY3			;Jump if less than 16 errors
0833  12 09 80  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0836  74 06     				MOV		A,#06h
0838  12 01 93  				LCALL	$TXBYTE					;End read 16 bytes from cmd file
083B  12 01 8B  				LCALL	$RXBYTE					;Wait for keypress
083E  D0 00     				POP		$00h					;Restore R0
0840  22        				RET
                
0841  90 00 00  PM_ISERASED:	MOV		DPTR,#0000h
0844  75 2F 00  				MOV		$2Fh,#00h
0847  74 30     PM_ISERASED1:	MOV		A,#30h
0849  12 09 58  				LCALL	$ISPCOMM				;Init read page mode
084C  E5 83     				MOV		A,DPH
084E  12 09 58  				LCALL	$ISPCOMM				;Send high address
0851  E4        PM_ISERASED2:	CLR		A
0852  12 09 58  				LCALL	$ISPCOMM				;Get byte from ROM
0855  04        				INC		A
0856  42 2F     				ORL		$2Fh,A
0858  A3        				INC		DPTR
0859  E5 82     				MOV		A,DPL
085B  70 F4     				JNZ		$PM_ISERASED2			;Jump if more bytes on this page
085D  E5 2F     				MOV		A,$2Fh
085F  70 05     				JNZ		$PM_ISERASED3
0861  E5 83     				MOV		A,DPH
0863  B5 26 E1  				CJNE	A,$26h,$PM_ISERASED1	;Jump if more pagees
0866  C3        PM_ISERASED3:	CLR		C
0867  E5 2F     				MOV		A,$2Fh
0869  60 01     				JZ		$PM_ISERASED4
086B  D3        				SETB	C
086C  22        PM_ISERASED4:	RET
                
086D  12 08 41  PM_ROMPROG:		LCALL	$PM_ISERASED			;Check if chip is erased
0870  50 4E     				JNC		$PM_ROMPROG1
0872  74 AC     				MOV		A,#0ACh
0874  12 09 58  				LCALL	$ISPCOMM				;Init chip erase byte 1
0877  74 80     				MOV		A,#80h
0879  12 09 58  				LCALL	$ISPCOMM				;Init chip erase byte 2
087C  E4        				CLR		A
087D  12 09 58  				LCALL	$ISPCOMM				;Init chip erase byte 3
0880  E4        				CLR		A
0881  12 09 58  				LCALL	$ISPCOMM				;Init chip erase byte 4
0884  E4        				CLR		A
0885  12 09 51  				LCALL	$WAIT					;Wait 256 ms
0888  E4        				CLR		A
0889  12 09 51  				LCALL	$WAIT					;Wait 256 ms
088C  E4        				CLR		A
088D  12 09 51  				LCALL	$WAIT					;Wait 256 ms
0890  12 08 41  				LCALL	$PM_ISERASED
0893  50 2B     				JNC		$PM_ROMPROG1
0895  12 09 80  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0898  12 00 D8  				LCALL	$PRNTSTR
089B  0B 2A 23  				DB		0Bh,2Ah,23h,'Could not erase chip <Enter> ',00h
08BC  12 01 8B  				LCALL	$RXBYTE					;Wait for keypress
08BF  22        				RET
08C0  C0 00     PM_ROMPROG1:	PUSH	$00h
08C2  90 00 00  				MOV		DPTR,#0000h
08C5  12 01 76  				LCALL	$RX16BYTES				;Read 16 bytes from cmd file. R0 points to start of 16 byte buffer
08C8  74 40     PM_ROMPROG2:	MOV		A,#40h
08CA  12 09 58  				LCALL	$ISPCOMM				;Init byte programming mode
08CD  E5 83     				MOV		A,DPH
08CF  12 09 58  				LCALL	$ISPCOMM				;Send high address
08D2  E5 82     				MOV		A,DPL
08D4  12 09 58  				LCALL	$ISPCOMM				;Send low address
08D7  E6        				MOV		A,@R0
08D8  F5 25     				MOV		$25h,A					;Get byte from buffer
08DA  12 09 58  				LCALL	$ISPCOMM				;Send byte to be programmed
08DD  74 10     				MOV		A,#10h
08DF  12 09 51  				LCALL	$WAIT					;Wait 1mS
08E2  12 09 E0  				LCALL	$BM_ROMRDBYTE			;Read a byte from ROM
08E5  B5 25 22  				CJNE	A,$25h,$PM_ROMPROG4		;Compare and jump if not equal
08E8  08        				INC		R0
08E9  E8        				MOV		A,R0
08EA  B4 50 03  				CJNE	A,#50h,$PM_ROMPROG3
08ED  12 01 76  				LCALL	$RX16BYTES				;Read 16 bytes from cmd file. R0 points to start of 16 byte buffer
08F0  A3        PM_ROMPROG3:	INC		DPTR
08F1  E5 82     				MOV		A,DPL
08F3  70 D3     				JNZ		$PM_ROMPROG2			;Jump if still on same page
08F5  74 2E     				MOV		A,#2Eh
08F7  12 01 93  				LCALL	$TXBYTE
08FA  E5 83     				MOV		A,DPH
08FC  B5 26 C9  				CJNE	A,$26h,$PM_ROMPROG2		;Jump if more pages
08FF  12 09 80  				LCALL	$ROMOFF					;Set RST low and turn off VCC
0902  74 06     				MOV		A,#06h
0904  12 01 93  				LCALL	$TXBYTE					;End read 16 bytes from cmd file
0907  D0 00     				POP		$00h
0909  22        				RET
090A  C0 E0     PM_ROMPROG4:	PUSH	ACC
090C  12 09 80  				LCALL	$ROMOFF					;Set RST low and turn off VCC
090F  74 06     				MOV		A,#06h
0911  12 01 93  				LCALL	$TXBYTE					;End read 16 bytes from cmd file
0914  12 00 D8  				LCALL	$PRNTSTR
0917  0B 2A 23  				DB		0Bh,2Ah,23h,'Error at ',00h
0924  E5 83     				MOV		A,DPH
0926  12 01 04  				LCALL	$HEXOUT					;High address
0929  E5 82     				MOV		A,DPL
092B  12 01 04  				LCALL	$HEXOUT					;Low address
092E  74 20     				MOV		A,#20h
0930  12 01 93  				LCALL	$TXBYTE
0933  E5 25     				MOV		A,$25h
0935  12 01 04  				LCALL	$HEXOUT					;Byte from .cmd file
0938  74 20     				MOV		A,#20h
093A  12 01 93  				LCALL	$TXBYTE
093D  D0 E0     				POP		ACC
093F  12 01 04  				LCALL	$HEXOUT					;Byte read from ROM
0942  12 01 8B  				LCALL	$RXBYTE					;Wait for a keypress
0945  D0 00     				POP		$00h					;Restore R0
0947  22        				RET
                
                ;Wait functions
                ;------------------------------------------------------------------
                
0948  C0 07     WAIT100:		PUSH	$07h					;Save R7
094A  7F 5C     				MOV		R7,#5Ch
094C  DF FE     WAIT1001:		DJNZ	R7,$WAIT1001			;Wait loop, 100uS
094E  D0 07     				POP		$07h					;Restore R7
0950  22        				RET
                
0951  CF        WAIT:			XCH		A,R7
0952  31 48     WAIT1:			ACALL	$WAIT100
0954  DF FC     				DJNZ	R7,$WAIT1
0956  CF        				XCH		A,R7
0957  22        				RET
                
                ;Control functions
                ;------------------------------------------------------------------
                
                ;IN A, OUT A
0958  C0 07     ISPCOMM:		PUSH	$07h
095A  C0 02     				PUSH	$02h
095C  7A 08     				MOV		R2,#08h
095E  33        ISPCOMM1:		RLC		A
095F  92 93     				MOV		P1.3,C					;MISO
0961  A2 94     				MOV		C,P1.4					;MOSI
0963  CF        				XCH		A,R7
0964  33        				RLC		A
0965  CF        				XCH		A,R7
0966  D2 92     				SETB	P1.2					;SCK H
0968  00        				NOP
0969  C2 92     				CLR		P1.2					;SCK L
096B  DA F1     				DJNZ	R2,$ISPCOMM1
096D  EF        				MOV		A,R7
096E  D0 02     				POP		$02
0970  D0 07     				POP		$07
0972  22        				RET
                
0973  D2 90     ROMON:			SETB	P1.0					;+5V On
0975  74 0A     				MOV		A,#0Ah
0977  31 51     				ACALL	$WAIT					;Wait 1mS
0979  D2 91     				SETB	P1.1					;RST H
097B  74 0A     				MOV		A,#0Ah
097D  31 51     				ACALL	$WAIT					;Wait 1mS
097F  22        				RET
                
0980  C2 91     ROMOFF:			CLR		P1.1					;RST L
0982  74 01     				MOV		A,#01h
0984  31 51     				ACALL	$WAIT					;Wait 100uS
0986  75 90 10  				MOV		P1,#10h					;+5V Off, P1.4 As Input
0989  74 0A     				MOV		A,#0Ah
098B  12 09 51  				LCALL	$WAIT					;Wait 1mS
098E  22        				RET
                
098F  74 AC     ROMINITPGM:		MOV		A,#0ACh
0991  12 09 58  				LCALL	$ISPCOMM
0994  74 53     				MOV		A,#53h
0996  12 09 58  				LCALL	$ISPCOMM
0999  74 00     				MOV		A,#00h
099B  12 09 58  				LCALL	$ISPCOMM
099E  74 00     				MOV		A,#00h
09A0  12 09 58  				LCALL	$ISPCOMM
09A3  B4 69 01  				CJNE	A,#69h,$ROMINITPGM1
09A6  22        				RET
09A7  12 00 D8  ROMINITPGM1:	LCALL	$PRNTSTR
09AA  0B 2A 23  				DB		0Bh,2Ah,23h,'Initialisation Error <Enter> ',00h
09CB  12 01 8B  				LCALL	$RXBYTE
09CE  D3        				SETB	C
09CF  22        				RET
                
09D0  90 00 00  ROMINIT:		MOV		DPTR,#0000h				;DPTR holds ROM address
09D3  12 09 73  				LCALL	$ROMON					;Turn on VCC and pull RST high
09D6  12 09 8F  				LCALL	$ROMINITPGM
09D9  50 04     				JNC		$ROMINIT1
09DB  12 09 80  				LCALL	$ROMOFF					;Init programming failed
09DE  D3        				SETB	C
09DF  22        ROMINIT1:		RET
                
09E0  74 20     BM_ROMRDBYTE:	MOV		A,#20h
09E2  12 09 58  				LCALL	$ISPCOMM
09E5  E5 83     				MOV		A,DPH
09E7  12 09 58  				LCALL	$ISPCOMM
09EA  E5 82     				MOV		A,DPL
09EC  12 09 58  				LCALL	$ISPCOMM
09EF  74 00     				MOV		A,#00h
09F1  12 09 58  				LCALL	$ISPCOMM
09F4  22        				RET
                
09F5  C0 E0     ROMVERIFYERR:	PUSH	ACC
09F7  12 00 D8  				LCALL	$PRNTSTR
09FA  0D 20 20  				DB		0Dh,'   Error at ',00h
0A08  E5 83     				MOV		A,DPH
0A0A  12 01 04  				LCALL	$HEXOUT
0A0D  E5 82     				MOV		A,DPL
0A0F  12 01 04  				LCALL	$HEXOUT
0A12  74 20     				MOV		A,#20h
0A14  12 01 93  				LCALL	$TXBYTE
0A17  E5 25     				MOV		A,$25h
0A19  12 01 04  				LCALL	$HEXOUT
0A1C  74 20     				MOV		A,#20h
0A1E  12 01 93  				LCALL	$TXBYTE
0A21  D0 E0     				POP		ACC
0A23  12 01 04  				LCALL	$HEXOUT
0A26  12 00 FB  				LCALL	$PRNTCRLF
0A29  05 2F     				INC		$2Fh
0A2B  E5 2F     				MOV		A,$2Fh
0A2D  B4 10 00  				CJNE	A,#10h,$00h
0A30  B3        				CPL		C
0A31  22        				RET
                
                ;------------------------------------------------------------------
                
                ;				ORG		2000h					;Fill up 2764
